buildscript {
    apply from: 'versions.gradle'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "me.tatarka:gradle-retrolambda:$retrolambda_version"
    }
}

allprojects {
    repositories {
        jcenter()
        maven { url 'https://maven.google.com' }
    }
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

android {
    signingConfigs {
        release {
            keyAlias 'key'
            keyPassword '123456'
            storeFile file('../cert/keystore.jks')
            storePassword '123456'
        }
    }
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "me.maximpestryakov.yamblzweather"
        minSdkVersion 19
        //noinspection OldTargetApi
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "me.maximpestryakov.CustomRunner"
    }
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true
            applicationIdSuffix ".debug"
        }
        prodDebug {
            minifyEnabled true
            shrinkResources true
            debuggable true
            applicationIdSuffix ".prodDebug"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules-debug.pro'
            signingConfig signingConfigs.release
        }
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
        resolutionStrategy.force 'com.android.support:support-annotations:25.4.0'
    }
    sourceSets {
        String sharedTestSrcDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestSrcDir
        }
        androidTest {
            java.srcDir sharedTestSrcDir
        }
    }
}

android.applicationVariants.all {
    def aptOutputDir = new File(buildDir, "generated/source/apt/${it.unitTestVariant.dirName}")
    it.unitTestVariant.addJavaSourceFoldersToModel(aptOutputDir)
}

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:2.8.0'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'com.squareup.okhttp3:mockwebserver:3.8.0'
    // Dagger for test
    testCompile "com.google.dagger:dagger-android:$dagger_version"
    testAnnotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"

    androidTestCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'com.google.dexmaker:dexmaker:1.1'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.1'
    androidTestCompile 'com.android.support.test:runner:1.0.0'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:3.0.0'
    androidTestCompile 'com.android.support.test.espresso:espresso-contrib:3.0.0'
    // Dagger for androidTest
    androidTestCompile "com.google.dagger:dagger-android:$dagger_version"
    androidTestAnnotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"

    // Support Library
    compile "com.android.support:appcompat-v7:$support_version"
    compile "com.android.support:recyclerview-v7:$support_version"
    compile "com.android.support:design:$support_version"
    compile "com.android.support:preference-v7:$support_version"

    // Moxy
    compile "com.arello-mobile:moxy:$moxy_version"
    compile "com.arello-mobile:moxy-app-compat:$moxy_version"
    annotationProcessor "com.arello-mobile:moxy-compiler:$moxy_version"

    // Butter Knife
    compile "com.jakewharton:butterknife:$butterknife_version"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknife_version"

    // Gson
    compile "com.google.code.gson:gson:$gson_version"

    // Retrofit
    compile "com.squareup.retrofit2:retrofit:$retrofit_version"
    compile "com.squareup.retrofit2:converter-gson:$retrofit_version"
    compile "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"

    // RxJava 2
    compile "io.reactivex.rxjava2:rxjava:$rxjava_version"

    // RxAndroid
    compile "io.reactivex.rxjava2:rxandroid:$rxandroid_version"

    // RxBinding
    compile "com.jakewharton.rxbinding2:rxbinding:$rxbinding_version"

    // Dagger
    compile "com.google.dagger:dagger-android:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"

    // Android Job
    compile "com.evernote:android-job:$androidjob_version"

    // Timber
    compile "com.jakewharton.timber:timber:$timber_version"
}
